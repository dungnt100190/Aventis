SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
EXECUTE spDropObject spPendenzCheck_PensionsalterFrau
GO
/*===============================================================================================
  $Archive: /Database/KISS4_BSS_MasterDev/Stored Procedures/spPendenzCheck_PensionsalterFrau.sql $
  $Author: Lloreggia $
  $Modtime: 16.12.09 13:52 $
  $Revision: 5 $
=================================================================================================
  Description
-------------------------------------------------------------------------------------------------
  SUMMARY:          Sucht nach Frauen, die in Kürze das Pensionsalter erreichen
    @AnzahlTage:    Anzahl Tage, bevor eine Frau das Pensionsalter erreicht
    @Pensionsalter: Normales Pensionsalter für Frauen
  -
  RETURNS:          Ein Datensatz mit anzulegenden Pendenzen
  -
  TEST:             EXEC dbo.spPendenzCheck_PensionsalterFrau
=================================================================================================
  $Log: /Database/KISS4_BSS_MasterDev/Stored Procedures/spPendenzCheck_PensionsalterFrau.sql $
 * 
 * 4     16.12.09 13:53 Lloreggia
 * Pendenzen werden nur für offene Leistungen erzeugt
 * 
 * 3     30.11.09 10:08 Lloreggia
 * Header angepasst, Unterscheidung Geschlecht
=================================================================================================*/

CREATE PROCEDURE dbo.spPendenzCheck_PensionsalterFrau
(
	@AnzahlTage INT = 90,
	@Pensionsalter INT = 63
)
AS
BEGIN
	-- SET NOCOUNT ON added to prevent extra result sets from
	-- interfering with SELECT statements.
	SET NOCOUNT ON;

  -- declare local variables
  DECLARE @AutoGeneratedType INT
  DECLARE @ReferenceTable VARCHAR(100)
  DECLARE @Code INT;

  SET @AutoGeneratedType = 3  -- PensionsalterFrau
  SET @ReferenceTable = 'BaPerson'
  SET @Code = 31 --Pensionsalter Frau

	SELECT DISTINCT
    TaskSenderCode    = 5,  -- DbScript
    TaskReceiverCode  = 1,  -- Person
    TaskTypeCode      = @Code, --Pensionsalter Frau
    TaskStatusCode    = 1,  -- Pendent
    CreateDate        = DATEADD(DAY, -@AnzahlTage, DATEADD(YEAR, @Pensionsalter, PRS.GeburtsDatum)),
    StartDate         = NULL,
    ExpirationDate    = DATEADD(YEAR, @Pensionsalter, PRS.GeburtsDatum),
    Subject           = (SELECT dbo.fnStringReplace(Value1, CONVERT(varchar, DATEADD(YEAR, @Pensionsalter, PRS.GeburtsDatum), 104)) FROM XLOVCode WHERE LOVName ='TaskType' AND Code = @Code),
    TaskDescription   = (SELECT Value2 FROM XLOVCode WHERE LOVName ='TaskType' AND Code = @Code),
    FaLeistungID      = FPL.FaLeistungID,
    BaPersonID        = FPP.BaPersonID,
    SenderID          = NULL,
    ReceiverID        = LEI.UserID,
    ReferenceTable    = @ReferenceTable,
    ReferenceID       = PRS.BaPersonID,
    AutoGeneratedType = @AutoGeneratedType
  FROM dbo.BgFinanzplan                  FPL WITH (READUNCOMMITTED)
    INNER JOIN dbo.BgFinanzplan_BaPerson FPP WITH (READUNCOMMITTED) ON FPL.BgFinanzplanID = FPP.BgFinanzplanID
                                                                   --AND FPP.IstUnterstuetzt = 1
    INNER JOIN dbo.BaPerson              PRS WITH (READUNCOMMITTED) ON PRS.BaPersonID = FPP.BaPersonID
                                                                   AND PRS.GeschlechtCode = 2 -- Frau
    INNER JOIN dbo.FaLeistung            LEI WITH (READUNCOMMITTED) ON FPL.FaLeistungID = LEI.FaLeistungID
  WHERE ISNULL(LEI.DatumBis, '99990101') >= GETDATE()
    AND FPL.BgBewilligungStatusCode >= 5
    AND DATEDIFF(DAY, GETDATE(), DATEADD(YEAR, @Pensionsalter, PRS.GeburtsDatum)) <= @AnzahlTage
    AND GETDATE() <= DATEADD(YEAR, @Pensionsalter, PRS.GeburtsDatum)
    AND NOT EXISTS (SELECT TOP 1 1
                    FROM dbo.XTaskAutoGenerated TAG WITH (READUNCOMMITTED)
                    WHERE TAG.XTaskAutoGeneratedTypeCode = @AutoGeneratedType
                      AND TAG.ReferenceTable = @ReferenceTable
                      AND TAG.ReferenceID = PRS.BaPersonID);

END
GO
